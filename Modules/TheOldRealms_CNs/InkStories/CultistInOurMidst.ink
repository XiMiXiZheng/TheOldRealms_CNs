//Global story tags
# title: 潜伏的邪教徒
# frequency: Special
# development: false
# illustration: village

INCLUDE include.ink
        
    VAR DealtWithCultists = false
    VAR CultName = "Cult of Khorne"
    VAR HardSkillCheckValue = 250
    VAR NormalSkillCheckValue = 150
    VAR EasySkillCheckValue = 80
    VAR ElderState = 0
        ~ ElderState = RANDOM(1,3) // 1 - normal, 2 - guilty, 3 - grumpy
    VAR SymbolLeftBehind = 1
        //~ SymbolLeftBehind = RANDOM(0,1)
    VAR CultIsKnownToPlayer = false
    VAR MassacreHappened = false
    VAR StruggleHappened = false

-> Start

===Start===
    前往村庄的路途险象环生，蜿蜒穿过茂密的森林和多雾的山谷。当你来到村庄时，你发现村民们熙熙攘攘地过着日常生活。然而，空气中藏着一丝不安的气息，强颜欢笑和低声私语的背后暗流涌动。
    你将队伍留在村外扎营，决定独自开始调查。你在暗处注意到一小群人聚集在村子广场附近，他们的举止可疑而神秘。他们交换着暗号眉来眼去，相互窃窃私语。
    ->choices

    =choices
    *[靠近他们，倾听他们的对话。{print_player_skill_chance("Roguery", NormalSkillCheckValue)}]
        {perform_player_skill_check("Roguery", NormalSkillCheckValue): ->ListenToGroup.succeed | -> ListenToGroup.fail}
    *[暗中与其他村民交流。] ->InvestigateVillagers


=== ListenToGroup ===

    =succeed
    (SUCCESS)
    你小心翼翼地靠近那群人，避免引起他们的注意。你站在远处，竖起耳朵捕捉他们的只言片语。
    
    “……召唤仪式必须尽快举行，”一个人焦急地低声说道。“我们的力量与日俱增。”
    
    另一个声音回应道：“必须隐藏自己。圣殿骑士可能会盯上我们。我们不想让猎魔人遍布村子，那样一切都完了。”
    
    “今晚在Cradle见面。”
    
    队伍散去，他们消失在人群当中。村民们继续过着日常生活，似乎对潜伏在他们当中的黑暗视而不见。
    ->Start.choices
    
    =fail
    (FAIL)
    你小心翼翼地靠近那群人，避免引起他们的注意。然而隐蔽并不是你的强项，当你一步步靠近时，突然一阵吱吱嘎嘎的声音，让他们注意到了你的存在。
    他们朝你的方向看了一眼，眯起眼来充满怀疑。快速交流了几句后，他们便消失在人群中。
    你的计划失败了，你不禁怀疑自己是否被他们盯上。
    ->Start.choices


=== InvestigateVillagers ===
    你意识到邪教徒善于隐藏自己的真实身份。要找出他们的身份绝非易事。您决定与村民交流，收集更多信息。
    您与不同的人交谈，巧妙地打听最近发生的怪事、失踪人口或黑暗习俗的传言。一些村民表达了不安，他们提到了刻在隐蔽角落的神秘符文、无法解释的失踪事件以及夜间森林中出现的奇怪灯光。
    ->choices

    =choices
    *[找村长问话。] ->InterviewElder
    *[夜间进入森林，找到奇异灯光的源头。] ->Woods
    *[与失踪者亲属交谈，调查失踪事件。] ->InvestigateDisappearances
    * -> OutOfOptions


=== InterviewElder ===
{ElderState == 3: ->grumpy | ->normal}

    =normal
    这个村长，相比简陋的村庄，他一定有不少钱。他的屋子在村中心属实牢固。
    你敲了敲木门，村长用温暖的微笑欢迎你进屋。小屋满是舒适的气息，噼啪作响的壁炉在房间里散发着令人愉悦的光芒。你在一张小木桌旁坐了下来，讨论你对村子里发生的怪事。
    {ElderState == 1:你向村长提出村里可能有邪教徒。他听得很认真，但脸上充满怀疑。他否定了邪教的说法，认为这既荒谬又牵强。他认为最近发生的麻烦只是巧合。}
    {ElderState == 1:“我理解你的担忧，”他的声音带着一丝傲慢，“但我向你保证，我们村里没有邪教。这些怪事可以用自然原因来解释，也可以用某些村民过于丰富的想象力来解释。”}
    {ElderState == 1:村长的否认让你感到沮丧，你意识到说服他对邪教采取行动将极为艰难。显然你需要探索其他方法来应对日益严重的威胁。}
    {ElderState == 2:谈话过程中，你观察周围的环境，注意到村长较为富裕的事实。银器在柔和的烛光下闪闪发光，墙壁上的画作展现了宁静的风景，村长的衣服也比普通村民的要华丽一些。}
    {ElderState == 2:随着讨论的深入，村长承认了村子里的麻烦，但他认为在村子里有邪教的想法太过荒谬了。然而，你从他的眼神中感受到了一丝不安，一丝内疚，这比他的言语所传达的信息要多得多。}
    ->choices
    
    =grumpy
    这位村长因在维持秩序和解决问题等方面杂乱无序而闻名，他住在村中心一间简陋的平房里。你靠近后发现屋子疏于打理——花园杂草丛生，前门油漆剥落，整体显得凌乱不堪。
    你敲了敲木门，村长面露怒意，抱怨道：“你想要什么？”语气很不友善。你解释了来访的原因，表达了对村里发生的怪事和可能存在邪教的担忧。
    “你认为我们村里有邪教？”他嗤之以鼻，完全不相信“太扯了！活着的麻烦事情够他妈多了，你还嫌不够是吧，竟瞎说。麻烦？是的，我们有很多麻烦。但邪教？你吃错药了吧？”
    当你进一步追问，试图让村长相信事情的严重性时，他的怒气瞬间上来了：“你扯够没有？老子还有更重要的事情去做！”他咆哮道，“你想干什么做什么你随意，别瞎拿邪教忽悠我，竟还把我当白痴看待！”
    说完，他当着你的面摔门而去，哐当声打碎本来宁静的街道。
    别无选择，你必须在没有村长合作的情况下找到其他调查方法。
    ->InvestigateVillagers.choices
    
    =choices
    *{ElderState == 2}[质疑村长的财富，指责他从事非法活动。 {print_player_skill_chance("Charm", HardSkillCheckValue)}]
        {perform_player_skill_check("Charm", HardSkillCheckValue): -> InterviewElder.succeed | -> InterviewElder.fail}
    * -> InvestigateVillagers.choices

    =succeed
    (SUCCESS)
    村长的脸抽搐了一下，对你直言不讳感到措手不及。
    气氛瞬间凝固，村长承认他一直收到匿名放在他家门口的小钱。钱的来源对他来说仍然是个谜，但他承认，为了换取这些贿赂，他对奇怪的事件视而不见。他的声音里充满了羞愧，他解释说，为了摆脱经济上的困境，让家人过上更好生活的生活，他无奈这么做。
    他看起来很真诚。你相信他确实不知道贿赂款的更多来源。
    尽管他不知情，但你还是恳求村长为自己的行为负责，断绝与神秘人的关系。你强调恢复村庄安全的重要性，敦促打击邪教。
    *[等待下一个送钱的人，跟踪他。] -> wait
    *[找到其他调查方法。] -> InvestigateVillagers.choices
    
    =fail
    (FAIL)
    村长矢口否认这些指控。他义愤填膺地为自己辩护，声称他的境况之所以相对改善，是因为精明的财务管理和在村外进行的投资。
    村长巧妙地回避了你的指控，将其归咎于谣言和村民们对他小有成就的嫉妒。
    你有所怀疑，虽然他表现得却很清白，但你对他的真实意图始终心存疑虑。
    ->InvestigateVillagers.choices
    
    =wait
    你决心揭开贿赂款背后的神秘面纱，于是你在老人家中盯梢，等待下一笔贿赂款的到来。夜幕降临，你仍然在耐心的等待，警惕地观察着神秘人的任何踪迹。但是，随着时间的流逝，你发现晚上没人来打扰过村长。
    你越来越感到沮丧和疲惫，开始怀疑这种方法的有效性。也许行贿者已经意识到你的存在，或者他已经更变了交货方式。没有任何重要线索或进展，这让你下定决心。
    你放弃监视，承认这条线索已经走进了死胡同。
    -> InvestigateVillagers.choices

===Woods===
    森林里有奇怪的灯光，你对此很感兴趣，决定趁着夜色深入森林。你的感官异常敏锐，武器蓄势待发，你在茂密的树叶间穿梭。
    
    随着你深入森林，灯光变得更加强烈和神奇。它的舞动轨迹和闪烁方式似乎是精心策划、有意为之。
    
    出乎你意料的是，你偶然发现了几大群不同寻常的萤火虫，它们发光的身体创造出令人惊叹的奇观。萤火虫在空中飞舞、盘旋，发出迷人的光芒，照亮了周围的树木。
    
    你意识到这些萤火虫就是神秘灯光的源头，于是敬畏地看着它们继续夜间表演。虽然它们不是你要找的邪教徒，但它们的存在提醒你世界的美丽和神奇。
    
    您感受到了和平与宁静，在继续调查之前，您花了一点时间欣赏这自然奇观。
    ->InvestigateVillagers.choices

===InvestigateDisappearances===
    你的首要任务是接近失踪者的亲属，倾听他们的心声，给予安慰和支持，同时谨慎地收集信息。每个故事都充满了痛苦和困惑，都有一些无法解释的共同点。黑暗传言四处流行，一股看神秘力量潜伏在村庄的暗处。
    紧迫感的压迫之下，你继续深入调查，寻找线索。您绘制了失踪发生地点的地图，并在临时调查板上做了标记。结果发现，事件集中发生在村庄外围和周围的树林里。
    ->choices
    
    =search
    带着寻找答案的决心，你开始调查失踪者的家，希望发现任何可能揭示他们消失的线索。当你走进每一户人家时，空气中都弥漫着悲伤和不安的气息，让你想起以前突然变故的往事。
    在其中一户人家中，你发现了挣扎的痕迹——被撞倒的椅子、破碎的花瓶和杂乱散落的物品。很明显，这里发生了一些不愉快的事情，这表明他们是被迫离开的，而不是自愿离开的。
    在另一户人家中，你发现了一些私人物品——一个珍贵的小饰品、一封写了一半的信、一本原主人最喜欢的书。这些私人物品也暗示了他们离去的突然性和意外性。
    显然，这些消失的人都是受害者，他们都是被强迫带走的。
    {SymbolLeftBehind == 1: 当你仔细调查有挣扎痕迹的住宅时，你在一片混乱中敏锐地发现了一样东西——作案者留下的物品。你在一张倒塌的桌子下面发现了一块破裂的护身符和一条断裂的链子，它明确无误地属于你一直在寻找的邪教组织。 -> identify_option}
    {SymbolLeftBehind == 0: 尽管你对失踪者的家进行了彻底调查，但没有发现更多重要线索或突破口。挣扎的痕迹和被遗弃的物品只会加深谜团，给你留下更多的问题而不是答案。当你意识到线索断绝时，沮丧感和无助感油然而生。 ->InvestigateVillagers.choices}
    
    =identify_option
    *[Identify the symbol. {print_player_attribute_chance("Intelligence",5)}] -> identify_check
    
    =identify_check
    {perform_player_attribute_check("Intelligence", 5): -> succeed | -> fail}
    
    =succeed
    (SUCCESS)
    ~ CultIsKnownToPlayer = true
    你一眼就认出这是{CultName}邪教。
    当你意识到这件物品的重要性时，一股寒意从你的脊背处袭来。这件物品与邪教相关，这证实他们直接参与了失踪事件。
    ->InvestigateVillagers.choices
    
    =fail
    (FAIL)
    尽管你对该符文进行了彻底研究，但仍无法确定其含义或意义。
    你没有找到进一步的重要线索或突破口。神秘的符文、挣扎的痕迹和被遗弃的物品只会加深谜团，给你留下更多的问题而不是答案。
    ->InvestigateVillagers.choices
    
    =choices
    *[仔细搜查失踪者的住所。] -> search


===OutOfOptions===

不知疲倦地追查各种线索，做出各种选择之后，你的成果收效甚微。挫败感和疲惫感开始袭来，让你徘徊在十字路口，不知何去何从。

未解之谜压得你喘不过气来，你发现自己站在村里的小酒馆前。温暖的窗户和诱人的香气在向你招手。也许你需要一个喘息的机会，一个整理思绪、重新评估策略的机会。

当你坐在酒馆里考虑下一步行动时，附近一群人的谈话引起了你的注意。村民们正在热火朝天地讨论第二天的聚会计划，这是一个难得的机会，整个村子的人都会聚在一起讨论最近发生的事情和大家关心的问题。

仔细一听，你得知聚会将在村里的广场上举行。这个消息引起了你的兴趣。这是聚集村民、察言观色的好机会，也许还能瞥见任何试图混入人群的邪教徒。

你不禁在想，这样的活动会带来什么样的可能性。一想到整个村子的人都聚集在一个地方，包括潜在的邪教徒，你的脑海中就会浮现出一连串的想法。

邪教徒会不会计划在这次聚会中采取行动？他们会利用人群推进自己的计划，还是只是在暗处观察，掩饰自己的真实意图？


*[参加聚会。] -> AttendGathering
*[放弃追查。这项调查已经耗费了你太多的时间。] -> VoluntaryEnd

=== AttendGathering ===
村里聚会的日子到了，空气中弥漫着期待和谨慎的混合气息。你来到村里的广场，全神贯注，生怕错过一个线索。熙熙攘攘的人群，熟悉的面孔，隐藏其中的诸多未知。当你穿梭在村民中间时，你保持着警惕，目光扫视着任何邪教的迹象。

突然，一个微妙的变化引起了你的注意。一股紧张的暗流在人群中徘徊，你注意到有几个人似乎有些格格不入……躲闪的眼神、掩饰的表情。你的眼睛本能地紧盯着他们的一举一动。当你瞥见藏在他们衣服里的匕首闪闪发光时，你的怀疑得到了证实。

随着事件发展愈发严重，你的心跳疯狂加速。邪教徒就在村民中间，隐蔽且全副武装。{CultIsKnownToPlayer:根据你对{CultName}的了解， | 他们}的意图明显不仅仅是观察，他们还计划发动袭击，制造混乱和暴力。

你该如何行动？

*[小心谨慎，抽走凶器。 {print_player_skill_chance("Roguery", HardSkillCheckValue)}] -> DisarmCultists
*[大声揭露，号召村民反抗。 {print_player_skill_chance("Charm", HardSkillCheckValue)}] -> ExposeCultists

=== ExposeCultists ===
{perform_player_skill_check("Charm", HardSkillCheckValue): -> succeed | -> fail}

    =succeed
    ~ StruggleHappened = true
    (SUCCESS)
    你鼓足勇气，在一片混乱中大声疾呼。威严的呐喊击碎一切和恐惧和混乱。你揭露了隐藏邪教徒的威胁，他们带着匕首，意图袭击毫无防备的村民。
    
    震惊和难以置信在人群中迅速扩散，曾经的朋友和邻居竟然是邪教徒。
    
    村民们被背叛了，更被被激怒了！他们对邪教徒的信任破灭，开始物力反抗邪教徒。战斗短暂而激烈，村民们被一个共同的目标所驱使——保护他们的村庄，摆脱邪教的掌控！
    
    在一片混乱中，少数邪教徒设法挣脱了村民的控制，消失在广场周围蜿蜒的街道上。
    
    其余邪教徒双拳难敌四手，很快就被制服了。尘埃落定后，广场上满是胜利和解脱的喜悦。
    ->choices

    =fail
    (FAIL)
    全村人都在欢呼的时候，你站了出来，急不可待地传达着你的信息。你的声音响彻云霄，吸引了周围人的注意力。你揭开了隐藏的威胁——他们之中存在全副武装、意图谋杀村民献祭邪神的邪教徒。
    
    但是，你的呼吁收效甚微。怀疑的情绪就像投进平静池塘的石子，在人群之中传开。村民们的眼睛眯成一条缝，眉头紧皱，互相交换着眼神，有些人甚至对这个在他们看来荒诞不经的故事嗤之以鼻。
    ->CultistsAct
    
    =choices
    *[追赶逃跑的邪教徒。] ->ChaseCultist

===DisarmCultists===
    你悄无声息地穿梭在村民中间，精心规划每一步动作，以免引起注意。邪教徒们有意识的站在人群边缘。当你接近第一个邪教徒时，你的心怦怦直跳，紧张和决心交织在一起。
{perform_player_skill_check("Roguery", HardSkillCheckValue): -> succeed | -> fail}

    =succeed
    (SUCCESS)
    当你走到邪教徒身边时，你的手指灵巧的移向藏匿的匕首。你迅速抽出寒光闪闪的匕首，而邪教徒却对你的举动浑然不觉。
    每一个动作都经过仔细斟酌，每一个感官都敏锐细致。夺走每一把刀既是一种胜利，也是一种警告，警告异教徒的愚蠢行为。
    一些邪教徒意识到有些不对劲。他们变得紧张起来，眼中闪烁着怀疑。当他们意识到自己的行为快被发现时，内心的恐慌油然而生。
    邪教徒们惊恐得交换了眼神，无声的交流透露出一个事实——他们不再可能出其不意袭击村民了。他们开始逃离人群，躲进广场周围迷宫般的街道。
    ->choices
    
    =fail
    (FAIL)
    当邪教徒的目光锁定到你的行动时，你的心瞬间跳了一下。他的眼中闪过一丝慌乱，随即做出了迅速的反应。他们猛吸一口气，试图从你手中夺走匕首。你出其不意的计划失败了，取而代之的是引起附近邪教徒注意和挣扎。
    ->CultistsAct
    
    =choices
    *[追赶逃跑的邪教徒。] ->ChaseCultist

===CultistsAct===
~ MassacreHappened = true
    隐藏在暗处的邪教徒抓住这个时机实施他们的计划。他们从人群边缘，拔出暗藏的匕首，怀着邪恶的目的聚在一起。
    还没等人们反应过来，邪教徒们就开始行动了。他们的利刃在日光下闪闪发光，毫不留情地虐杀无辜的村民，村子陷入一片混乱。村子的广场变成噩梦般的屠宰场，空气中充斥着恐怖的惊呼和凄惨的哭喊，如同地狱。邪教徒们那令人不寒而栗的屠杀效率，让极度震惊的村民们的双腿生根无法移动。
    混乱和恐怖笼罩着村庄，你注意到一些奇怪的东西——鹅卵石上流淌的鲜血中出现了一些图案。邪教徒们蓄意挥舞的匕首划出一道道飞溅的鲜血，鲜血汇聚成小溪有意识的聚到一起。
    当你意识到这些图案的意义时，不禁打了个寒颤——邪教徒的意图远比单纯的屠杀要阴险得多。这些鲜血显然另有作用。他们的黑暗仪式，让溢出的鲜血渗入村庄广场下方的隐秘场所，这是一种安抚嗜血邪神的恐怖仪式。

*[领导部分遵命进行抵抗。] -> RallyVillagers
*[保住自己的小命，逃离村庄。]
    大屠杀是如此恐怖如此血腥，你的本能占据上风，逼迫着你采取行动。
    你转身离开现场，肾上腺素在你的血管中流淌。村民们的尖叫声在你耳边回荡，刺激着你迈开双腿，拼命寻找逃生之路。
    你离开了村庄。混乱和暴力随着谜题永远留在了村庄里，但你仍是不是会梦见那场血祭……
    ->END

===RallyVillagers===
一片血腥混乱之中，你稳住阵脚，依然坚定不移。你发出震耳欲聋的呐喊，带着紧迫感和权威性。

“全村的幸存者，联合起来！”你坚定的话语稳住了惊慌的村民，命令他们利用附近的支架和桌子筑起路障，抵御邪教徒的进攻。

一道道明确有力的指令下达，村民们有效组织起了反抗的阵地。他们设置的障碍形成了一道防线，并用临时武器对邪教徒们造成很大的麻烦。

邪教徒的进攻遇到了意想不到的抵抗，他们的决心开始动摇。村民们的联合阵线和他们取得的战略优势让邪教徒们束手无策。邪教徒们出其不意的进攻已被挫败，取而代之的是他们始料未及的反抗力量。

他们迅速撤退，逐渐消失在远方。
->choices

    =choices
    *[追赶逃跑的邪教徒。] ->ChaseCultist
    
===ChaseCultist===
    你可不会放过任何一个邪教徒，当他们抱头鼠窜的时候，你选择跟踪他们。蜿蜒的街道和狭窄的小巷变得模糊不清，你在迷宫般的道路上穿梭，渴望寻求答案，伸张正义。
    他们的足迹最终将你引向一座格格不入的建筑——一座被时间侵蚀而破败不堪的废弃屋子。入口被破旧的藤蔓帘幕遮挡，黑暗邪祟隐隐约约透露出了。邪教徒的脚步声逐渐消失，他们进入这个不祥的入口后消失了。
    长剑在手，不破邪教终不还！
->EnterHideout

===VoluntaryEnd===
    你重重地叹了口气，这个谜题对你来说实在太难，剥夺了你的宝贵时间和平静时光。你决定让村庄听天由命。你最后看了一眼，转身离去，未知的真相和未实现的正义是你耿耿于怀的负担。
->END

===EnterHideout===
~ OpenCultistLairMission("TOR_cultist_lair_001")
...
{DealtWithCultists: 正义的光辉随着刀剑相交，你的怒火势不可挡。当最后一个邪教徒倒在你的剑下时，整个陷入一片寂静。你站在邪教徒尸体的中间，满身污血，手中的剑见证了你坚定不移的决心和炉火纯青的技艺。}
{DealtWithCultists && MassacreHappened: 当你走出地下密室，看到村子广场上的景象时，你的心猛地一沉。曾经生机勃勃的村子如今变成尸山血海。村民和邪教徒的尸体散落一地，他们的生命在激烈的冲突中被扼杀了。}
{DealtWithCultists && MassacreHappened: 鲜血染红了鹅卵石，地面宛如地狱绘卷。血腥味和铁锈味如此刺鼻，与不久前广场上的欢庆形成鲜明对比。残骸、尸体、掀翻的看台、破碎的桌子见证了这场悲剧。}
{DealtWithCultists && MassacreHappened: 曾经生机勃勃的气氛被阴森恐怖的寂静所取代，只有远处传来的啜泣声和幸存者的轻声哭泣。面对邪教徒的威胁，村民们曾团结在你的身边，但现在他们要面对一个残酷的现实——胜利的代价过于沉重。}
{DealtWithCultists && not MassacreHappened && not StruggleHappened: 你从藏身处出来，踏入广场，与邪教徒的对决仍让你心有余悸。}
{DealtWithCultists && not MassacreHappened && not StruggleHappened: 你穿过人群时，发现自己的行动没有被注意到。村民们谈笑风生，沉浸在节日的喜庆中，丝毫没有意识到一场即将发生的血腥屠杀。}
{DealtWithCultists && not MassacreHappened && not StruggleHappened: 你花了一点时间欣赏空气中弥漫的温暖和欢快的氛围。孩子们在嬉戏，大人们在谈笑，村民们的友情依旧。你的行动保护了他们的生活。}
 {DealtWithCultists && not MassacreHappened: 邪教已被铲除，威胁已被扑灭。老树总会发芽，村庄也会重建，恢复往日的生机勃勃。}
 {not DealtWithCultists: 你战败了。你无力的靠在墙边，呼吸困难，遍体鳞伤。失败的沉重感向你袭来。冰冷的石墙见证了惨烈的战斗，黑暗占据上风。}
 {not DealtWithCultists: 安静……寂静……一个遥远的声音传入你的耳朵逐渐放大。那是急促的脚步声和高亢的嘶吼声，你村外的部队正赶来支援你。}
 {not DealtWithCultists: 邪教徒们曾设法杀死你，但他们已经暴露了自己，再不撤离迟早要完。便悄然离去，如同风吹起的烟雾一样消散。}
 {not DealtWithCultists: 邪教徒们一定会在另一个毫无防备的村庄继续他们的黑暗统治，这是一个令人痛苦的现实。}
->END